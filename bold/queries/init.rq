PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX arena: <https://solid.ti.rw.fau.de/public/ns/arena#>
PREFIX sim: <http://ti.rw.fau.de/sim#>

DELETE {
} INSERT {
    GRAPH ?transporter {
        ?transporter arena:locationX ?posX ;
            arena:locationY ?posY ;
            arena:tile ?tile ;
            arena:tileN ?tileN ;
            arena:tileNE ?tileNE ;
            arena:tileE ?tileE ;
            arena:tileSE ?tileSE ;
            arena:tileS ?tileS ;
            arena:tileSW ?tileSW ;
            arena:tileW ?tileW ;
            arena:tileNW ?tileNW .
    }
} WHERE {
    ?transporter a arena:Transporter .

    ?shopfloor a arena:Shopfloor ;
        arena:sizeX ?sizeX ;
        arena:sizeY ?sizeY .

    BIND(xsd:integer(FLOOR(sim:rand(?transporter)*?sizeX)) AS ?posX)
    BIND(xsd:integer(FLOOR(sim:rand(?transporter)*?sizeY)) AS ?posY)

    BIND(?posX - 1 AS ?posXminus1)
    BIND(?posX + 1 AS ?posXplus1)
    BIND(?posY - 1 AS ?posYminus1)
    BIND(?posY + 1 AS ?posYplus1)

    BIND(URI(CONCAT("/shopfloor/",xsd:string(?posX),"/",xsd:string(?posY))) AS ?tile)
    BIND(URI(CONCAT("/shopfloor/",xsd:string(?posX),"/",xsd:string(?posYminus1))) AS ?tileN)
    BIND(URI(CONCAT("/shopfloor/",xsd:string(?posXplus1),"/",xsd:string(?posYminus1))) AS ?tileNE)
    BIND(URI(CONCAT("/shopfloor/",xsd:string(?posXplus1),"/",xsd:string(?posY))) AS ?tileE)
    BIND(URI(CONCAT("/shopfloor/",xsd:string(?posXplus1),"/",xsd:string(?posYplus1))) AS ?tileSE)
    BIND(URI(CONCAT("/shopfloor/",xsd:string(?posX),"/",xsd:string(?posYplus1))) AS ?tileS)
    BIND(URI(CONCAT("/shopfloor/",xsd:string(?posXminus1),"/",xsd:string(?posYplus1))) AS ?tileSW)
    BIND(URI(CONCAT("/shopfloor/",xsd:string(?posXminus1),"/",xsd:string(?posY))) AS ?tileW)
    BIND(URI(CONCAT("/shopfloor/",xsd:string(?posXminus1),"/",xsd:string(?posYminus1))) AS ?tileNW)
};
